set PREVIEW_MODE "JPN n3DS Filtering Disabler\n \nMade by github/windows-server-2003\nForked by github/amplesand"

set MSG_ERR_O3DS "O3DS is not supported."
set MSG_ASK_GM9VER "This script requires GodMode9 v1.9.2pre1 or above.\nYou are running GodMode9 $[GM9VER].\nDo you wish to procede?"
set MSG_CONFIRM_NONJPN_REGION "Non-JPN region detected.\n \nDo you wish to procede regardless?"
set MSG_ERR_NO_SAVEDATA "Browser savedata not found.\n \nVerify that you have opened the browser at least once."
set MSG_ERR_FAILED_TO_MOUNT "Invalid browser savedata. (failed to mount)"
set MSG_ASK_ENABLE "The browser filter is currently disabled.\n \nDo you wish to enable it?"
set MSG_ASK_DISABLE "The browser filter is currently enabled.\n \nDo you wish to disable it?"
set MSG_ERR_UNKNOWN_VALUE "Invalid browser savedata.\n0x3090:"
set MSG_PERMISSION_DENIED "Permission denied."
set MSG_SUCCESS_DISABLING "Successfully disabled the browser filter."
set MSG_SUCCESS_ENABLING "Successfully enabled the browser filter."

# https://www.3dbrew.org/wiki/Internet_Browser#0x000200BB_savedata
# It seems that 0x3090-th byte of partitionA.bin is 00 by default, and 01 if the browser filter is disabled.
set SAVE_PATH 1:/data/$[SYSID0]/sysdata/000200bb/00000000

# Version warning
ask $[MSG_ASK_GM9VER]

# Precheck
if chk $[ONTYPE] O3DS
	echo $[MSG_ERR_O3DS]
	goto FAIL
end
if not chk $[REGION] JPN
	ask -s $[MSG_CONFIRM_NONJPN_REGION]
end
if not find $[SAVE_PATH] NULL
	echo $[MSG_ERR_NO_SAVEDATA]
	goto FAIL
end

imgumount
if not imgmount $[SAVE_PATH]
	echo $[MSG_ERR_FAILED_TO_MOUNT]
	goto FAIL
end

fget D:/partitionA.bin@3090:1 FILTERING_DISABLED_VALUE
if chk $[FILTERING_DISABLED_VALUE] 00 # Currently enabled
	set ASK_SENTENCE $[MSG_ASK_DISABLE]
	set SUCCESSMSG $[MSG_SUCCESS_DISABLING]
	set WRITE_VALUE 01
elif chk $[FILTERING_DISABLED_VALUE] 01 # Currently disabled
	set ASK_SENTENCE $[MSG_ASK_ENABLE]
	set SUCCESSMSG $[MSG_SUCCESS_ENABLING]
	set WRITE_VALUE 00
else
	echo $[MSG_ERR_UNKNOWN_VALUE]$[FILTERING_DISABLED_VALUE]
	goto FAIL
end
if not ask $[ASK_SENTENCE]
	goto FAIL
end

# Acquire permission
if not allow $[SAVE_PATH]
	echo $[MSG_PERMISSION_DENIED]
	goto FAIL
end

fset D:/partitionA.bin@3090 $[WRITE_VALUE]
goto EXIT

@FAIL
imgumount
chk -s a b

@EXIT
imgumount

